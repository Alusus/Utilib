import "Srl/String";
import "Srl/System";
import "Srl/Time";
import "Core/Data";
import "Spp";

if not Srl.String.isEqual(Process.platform, "windows") {
    Core.importFile("Termios.alusus");
}

@merge module Utilib {
    use Srl;

    //=============
    // Misc Helpers

    macro charsPtrOrDefault [val, default] CharsPtr().{
        this = val;
        if this == 0 this = default;
    }

    macro envVarOrDefault[varName, defaultVal] charsPtrOrDefault[System.getEnv(varName), default];

    function getSystemLanguage(): String {
        def langEnv: CharsPtr = System.getEnv("LANGUAGE");
        if langEnv == 0 || String.getLength(langEnv) == 0 langEnv = System.getEnv("LANG");
        if langEnv == 0 || String.getLength(langEnv) == 0 langEnv = "en";

        def lang: String;
        lang.assign(langEnv, 2);
        return lang;
    }

    //=======================
    // AST Generation Helpers

    module Ast {
        macro strLiteralFromVal [val] preprocess {
            Spp.astMgr.insertAst(Core.Data.Ast.StringLiteral(val));
        }

        macro strLiteralFromVal [val, default] strLiteralFromVal[charsPtrOrDefault[val, default]];

        macro strLiteralFromEnvVar [varName] strLiteralFromVal[System.getEnv(varName)];

        macro strLiteralFromEnvVar [varName, default] strLiteralFromVal[System.getEnv(varName), default];

        macro intLiteralFromInt [val] preprocess {
            Spp.astMgr.insertAst(Core.Data.Ast.IntegerLiteral(String() + val));
        }

        macro intLiteralFromCharsPtr [val] preprocess {
            Spp.astMgr.insertAst(Core.Data.Ast.IntegerLiteral(val));
        }

        macro intLiteralFromCharsPtr [val, default] intLiteralFromCharsPtr[charsPtrOrDefault[val, default]];

        macro intLiteralFromEnvVar [varName] intLiteralFromCharsPtr[System.getEnv(varName)];

        macro intLiteralFromEnvVar [varName, default] intLiteralFromCharsPtr[System.getEnv(varName), default];
    }

    //==================
    // Time/Date Helpers

    function getCurrentStringDateTime () => String {
        return getCurrentStringDateTime(Time.getTimestamp(0));
    }

    function getCurrentStringDateTime (timestamp: ArchInt) => String {
        def time: Time.DetailedTime;
        Time.getDetailedTimeØ›(timestamp~ptr, time~ptr);
        def buf: array[Char, 100];
        String.assign(
            buf~ptr, "%04d-%02d-%02d %02d:%02d:%02d",
            time.year + 1900, time.month + 1, time.day, time.hour, time.minute, time.second
        );
        return String(buf~ptr);
    }

    //================
    // Console Helpers

    module Console {
        preprocess {
            if String.isEqual(Process.platform, "windows") {
                Spp.astMgr.insertAst(ast {
                    @expname[_getch] function getStealthChar (): Int;
                });
            } else {
                Spp.astMgr.insertAst(ast {
                    function getStealthChar (): Int {
                        use Termois;

                        // Terminal flag constants
                        def ECHO: 0x00000008;
                        def ICANON: 0x00000002;
                        def TCSANOW: 0;

                        // Get current terminal attributes
                        def oldAttr: Attr;
                        def newAttr: Attr;
                        getAttr(0, oldAttr);

                        // Copy to new attributes
                        newAttr = oldAttr;

                        // Disable echo and canonical mode
                        newAttr.lFlag = newAttr.lFlag & ((ECHO | ICANON) $ -1);

                        // Set new attributes
                        setAttr(0, TCSANOW, newAttr);

                        // Read character
                        def ch: Char = Srl.Console.getChar();

                        // Restore old attributes
                        setAttr(0, TCSANOW, oldAttr);

                        return ch;
                    }
                });
            }
        }

        function getStealthString(): String {
            def str: String;

            while 1 {
                def ch: Char = getStealthChar();

                if ch == 10u || ch == 13u {
                    // Enter key (newline or carriage return)
                    Srl.Console.print("\n");
                    break;
                } else if ch == 127u || ch == 8u {
                    // Backspace or Delete - handle UTF-8 properly
                    if str.getLength() > 0 {
                        // Find the start of the last UTF-8 character
                        def len: ArchInt = str.getLength();
                        def i: ArchInt = len - 1;

                        // Skip UTF-8 continuation bytes (10xxxxxx = 0x80-0xBF)
                        while i > 0 && (str(i)~cast[Word[8]] & 0xC0) == 0x80 {
                            i -= 1;
                        }

                        // Remove from the start of the last character to the end
                        str = str.slice(0, i);
                    }
                } else {
                    // Regular character
                    str += ch;
                }
            }

            return str;
        }
    }
}
